plugins {
    id 'java'
    id 'idea'
    id "com.github.ben-manes.versions" version "0.36.0"
    id 'com.github.spotbugs' version '4.6.0'
}

group 'io.github.sebastian-0'
version '0.5-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'com.github.oshi:oshi-core:5.5.0'
    implementation 'net.java.dev.jna:jna:5.7.0'
    implementation 'net.java.dev.jna:jna-platform:5.7.0'
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation 'com.miglayout:miglayout-core:5.2'
    implementation 'com.miglayout:miglayout-swing:5.2'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

spotbugs {
    ignoreFailures = true
    excludeFilter = file('SpotBugsExclude.xml')
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

task copyToLib( type: Copy ) {
    into "$buildDir/libs/lib"
    from configurations.runtimeClasspath
}

task copyTextures( type: Copy ) {
    into "$buildDir/libs/textures"
    from "$projectDir/textures"
    include "*.png"
}

jar {
    manifest {
        attributes 'Main-Class':'taskmanager.ui.TaskManager',
                   'Class-Path': configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(' ')
    }
    dependsOn copyToLib, copyTextures
}

task packageDistibution(type: Zip) {
    archiveFileName = "${project.name}-${project.version}.zip"
    destinationDirectory = file("$buildDir")

    from "$buildDir/libs"
}

build {
    dependsOn packageDistibution
}